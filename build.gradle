plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'io.github.0ffz.github-packages' version '1.2.0'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
//    apply plugin: 'maven-publish'
    apply plugin: 'kotlin'
    apply plugin: 'kotlinx-serialization'

    group 'com.mineinabyss'
    version plugin_version

    kotlin {
        explicitApi()
    }

    repositories {
        mavenCentral()
        jcenter()
        maven githubPackage.invoke("MineInAbyss/KotlinSpice")
    }

    dependencies {
        compileOnly platform("com.mineinabyss:kotlinspice:$kotlin_version-10")
        compileOnly "org.jetbrains.kotlinx:kotlinx-serialization-cbor"

        implementation "com.mineinabyss:idofront:0.4.3"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += "-Xinline-classes"
            freeCompilerArgs += "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi"
        }
    }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

java {
    sourceCompatibility(JavaVersion.VERSION_1_8)
    withSourcesJar()
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    //ecs-related libs
    implementation "net.onedaybeard.bitvector:bitvector-jvm:0.1.4"
    implementation 'com.zaxxer:SparseBitSet:1.1'
    implementation 'org.clapper:javautil:3.2.0'

//    kapt 'com.bennyhuo.kotlin:deepcopy-compiler:1.3.72'
//    implementation 'com.bennyhuo.kotlin:deepcopy-runtime:1.3.72'
//    implementation 'net.onedaybeard.artemis:artemis-odb:2.3.0'
}

publishing {
    repositories {
        maven githubPackagePublish.invoke("MineInAbyss/Geary")
    }
    publications {
        gpr(MavenPublication) {
            artifactId = "geary"
            from components.java
        }
    }
}
