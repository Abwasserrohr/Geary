plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
//    apply plugin: 'maven-publish'
    apply plugin: 'kotlin'
    apply plugin: 'kotlinx-serialization'

    group 'com.mineinabyss'
    version plugin_version
    if (System.getenv("GITHUB_RUN_NUMBER") != null) version += ".${System.getenv("GITHUB_RUN_NUMBER")}"

    kotlin {
        explicitApi()
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://repo.mineinabyss.com/releases'}
    }

    dependencies {
        compileOnly platform("com.mineinabyss:kotlinspice:$kotlin_version+")
        compileOnly "org.jetbrains.kotlinx:kotlinx-serialization-cbor"

        implementation "com.mineinabyss:idofront:0.5.8"

    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                    "-Xinline-classes",
                    "-Xopt-in=kotlin.RequiresOptIn",
                    "-Xopt-in=kotlin.ExperimentalUnsignedTypes",
                    "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi"
            ]
        }
    }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

java {
    sourceCompatibility(JavaVersion.VERSION_1_8)
    withSourcesJar()
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    //provided by Minecraft
    compileOnly 'fastutil:fastutil:5.0.9'

    //ecs-related libs
    implementation('com.uchuhimo:kotlinx-bimap:1.2') {
        exclude group: 'org.jetbrains.kotlin'
    }

    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation 'io.kotest:kotest-runner-junit5:4.4.2'
    testImplementation 'io.kotest:kotest-property:4.4.2'
    testImplementation "io.mockk:mockk:1.10.6"
//    kapt 'com.bennyhuo.kotlin:deepcopy-compiler:1.3.72'
//    implementation 'com.bennyhuo.kotlin:deepcopy-runtime:1.3.72'
//    implementation 'net.onedaybeard.artemis:artemis-odb:2.3.0'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            url = "https://repo.mineinabyss.com/releases"
            credentials {
                username = findProperty('mineinabyssMavenUsername')
                password = findProperty('mineinabyssMavenPassword')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = "geary"
            from components.java
        }
    }
}
