plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.github.johnrengelman.shadow'
}

apply from: 'https://raw.githubusercontent.com/MineInAbyss/shared-gradle/master/common.gradle'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.codemc.io/repository/nms/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url("https://dl.bintray.com/korlibs/korlibs") }
    maven { url 'https://jitpack.io' }
}


dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    //TODO decide whether we stick with spigot or not since paper adds some nice things
    compileOnly "com.destroystokyo.paper:paper-api:$server_version"
    compileOnly "com.destroystokyo.paper:paper:$server_version" // NMS
    implementation "org.reflections:reflections:0.9.12"
    //TODO I'd like to use kotlinspice here but not sure how to best add dependencies that need to be shaded.
    // For now leave as compile only since this dep is always present and having 2 copies was causing issues.
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "com.mineinabyss:idofront-nms:0.5.8"
//    compileOnly "org.spigotmc:spigot-api:$server_version"
    compileOnly "com.github.okkero:skedule"

    api project(":")
}

shadowJar {
    archivesBaseName = "Geary"

    dependencies {
        exclude(dependency('org.jetbrains.kotlin:kotlin-stdlib:.*'))
    }

    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront".toLowerCase()
}
build.dependsOn shadowJar

publishing {
    repositories {
        maven {
            url = "https://repo.mineinabyss.com/releases"
            credentials {
                username = findProperty('mineinabyssMavenUsername')
                password = findProperty('mineinabyssMavenPassword')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = "geary-spigot"
            from components.java
        }
    }
}
